\documentclass[journal, retainorgcmds]{IEEEtran}
\usepackage{cite}
\usepackage[cmex10]{amsmath}
\usepackage{array}
\usepackage{url}

\usepackage{amssymb}
\usepackage{bbm}
\usepackage[greek,english]{babel}
\usepackage{textgreek}
\usepackage{ucs}
\usepackage[utf8x]{inputenc}
\usepackage{autofe}
\usepackage{minted}
\usepackage{fancyvrb}

% Missing unicode characters for Agda
\usepackage{MnSymbol}
\DeclareUnicodeCharacter{"2237}{$\squaredots$}
\DeclareUnicodeCharacter{"25C2}{$\blacktriangleleft$}
\DeclareUnicodeCharacter{"25B8}{$\blacktriangleright$}
\DeclareUnicodeCharacter{"1D9C}{$^{c}$}

% More readable mintinline
\newcommand{\iagda}[1]{\mintinline{agda}{#1}}
\newcommand{\ihask}[1]{\mintinline{haskell}{#1}}

% correct bad hyphenation here
\hyphenation{op-tical net-works semi-conduc-tor}

\begin{document}

\title{Ask not what types can do for you}
\author{Toon~Nolten,\\ \texttt{<toon.nolten@student.kuleuven.be>}}%
\maketitle


\begin{abstract}
  Dependent types are a powerful construct founded in type theory.
  Their expressiveness comes from the Curry-Howard correspondence \cite{cuho}.
  Originally this was applied in proof assistants \cite{proofass}, a property
  is represented as a type and its proof is a function of that type.
  This also works the other way around, we can embed proofs in our programs and
  in doing so prevent certain bugs from passing a static verification.
  Several languages have dependent types, most of them are on a spectrum
  between being a proof assistant and a programming language, Agda leans more
  towards the latter.
  Haskell with its Hindley-Milner type system is not dependently typed but
  extensions to GHC have been developed that borrow concepts and fit them into
  the existing system.
  In this paper we look at how dependent types can be used to add some
  verification to the implementation of red-black trees.
  This is done once for a fully dependently typed language, i.e. Agda, and in
  comparison in Haskell with the appropriate GHC extensions.
  Although these implementations are longer than the original from Chris
  Okasaki, they prevent us from violating some of the red-black tree invariants
  which is exactly what they were supposed to accomplish.
\end{abstract}

\begin{IEEEkeywords}
  Dependent Types, Agda, Haskell, Red-Black Trees.
\end{IEEEkeywords}


\section{Introduction}
\IEEEPARstart{D}{ependent} types are a powerful tool to improve software
development.
Type systems in general are used to prevent certain simple but hard to spot
bugs, e.g. a function that returns an integer instead of a floating point
number.
Dependent types are more expressive than simple types because they can
\emph{depend} on values.
The canonical introductory example of a dependent type is the type for vectors
(lists of fixed length):

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  data Vec (A : Set) : ℕ → Set where
    []  : Vec A zero
    _∷_ : ∀ {n} (x : A) (xs : Vec A n)
          → Vec A (suc n)
\end{minted}

For someone with a background in functional programming languages this may be
a good example of how dependent types are more expressive but for someone with
a background in imperative languages a list with a fixed length is just an
array.
Hopefully the running example in this paper, i.e. red-black trees
\cite{rbtrees}, will serve to convince those yet unimpressed of the
expressiveness of dependent types.

\hfill July 15, 2015

\subsection{Red-Black Trees}

Red-black trees are (approximately) balanced binary search trees.
Relying on color information (essentially an extra bit of data per node) to
maintain balance.
The approximate balancing is good enough to guarantee $\mathcal{O}(\log{}n)$
time complexity for search, insertion and deletion.
Binary search trees are often used to implement more abstract data structures
such as sets and maps.
Especially for ordered sets and maps this approach has advantages when compared
to a hash table based implementation as search trees maintain order by
definition.

An elegant implementation of red-black trees in a functional programming
language is that by Chris Okasaki \cite{okasaki}, in Haskell \cite{haskell},
which is what the implementations in the examples are based on:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  data Color = R | B
  data Tree elt =
    E | T Color (Tree elt) elt (Tree elt)

  type Set a = Tree a

  empty :: Set elt
  empty = E

  member :: Ord elt => elt -> Set elt -> Bool
  member x E = False
  member x (T _ a y b) | x <  y = member x a
                       | x == y = True
                       | x >  y = member x b

  -- originally without type signature
  balance :: Color -> Tree elt -> elt
             -> Tree elt -> Tree elt
  balance B (T R (T R a x b) y c) z d =
    T R (T B a x b) y (T B c z d)
  balance B (T R a x (T R b y c)) z d =
    T R (T B a x b) y (T B c z d)
  balance B a x (T R (T R b y c) z d) =
    T R (T B a x b) y (T B c z d)
  balance B a x (T R b y (T R c z d)) =
    T R (T B a x b) y (T B c z d)
  balance color a x b = T color a x b

  insert :: Ord elt => elt -> Set elt
            -> Set elt
  insert x s = makeBlack (ins s)
    where
      ins E = T R E x E
      ins (T color a y b)
        | x <  y = balance color (ins a) y b
        | x == y = T color a y b
        | x >  y = balance color a y (ins b)

      makeBlack (T _ a y b) = T B a y b
\end{minted}

Okasaki based his implementation on a succinct formulation of the red-black tree
invariants:
\begin{LaTeXdescription}
  \item[Invariant 1] No red node has a red parent.
  \item[Invariant 2] Every path from the root to an empty node contains the
                     same number of black nodes.
\end{LaTeXdescription}
While this implementation is short and simple enough for most people with some
functional programming experience to understand, it is really important for
data structures to be correctly implemented.
This is usually achieved by writing a suite of tests that exercise the
implementation.
Even though this is a proven methodology, there is undeniably some maintenance
overhead involved: tests need to be written and updated when the interface
changes.
Another disadvantage of this approach is that it is difficult to ensure that
each and every possible case is tested, usually tests are aimed at corner cases
that are identified by simply reasoning about the code.
Randomized testing tools such as QuickCheck \cite{quickcheck} were created for
precisely these reasons.
In the examples that follow the expressiveness of dependent types is exploited
to provide static verification of the correctness of the implementations.


\section{Red-Black Trees in Agda}

No matter how elegant Okasaki's implementation, the \ihask{Tree} data type
in no way prevents misuse, e.g. \ihask{T R (T R E 1 E) 2 E} and
\ihask{T B (T B E 1 E) 2 E} are both valid values of the type \ihask{Tree}
while they don't satisfy the invariants on red-black trees.
Because of this we have to be careful when implementing a function that
produces a \ihask{Tree}.
Not to mention how we should handle invalid red-black trees?
The easiest solution is not to think about it but then a runtime error could
occur because somewhere we defined a function with the implicit assumption it
would receive a valid red-black tree and it does not have a matching clause for
an invalid tree.
The other possible outcome is that it does not cause an error, every function
an invalid tree passes through may have done something and all we see is the
end result, this could be a debugging nightmare.
The expressiveness of dependent types can ensure that only valid red-black
trees can be created, in a way the \iagda{Tree} type will more accurately
represent all possible red-black trees:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  data Color : Set where
    R B : Color
  Height = ℕ

  data Tree : Color → Height → Set a where
    E : Tree B 0
    R : ∀{h} → Tree B h → A → Tree B h
        → Tree R h
    B : ∀{cl cr h} → Tree cl h → A → Tree cr h
        → Tree B (suc h)
\end{minted}

Instead of the algebraic data type (ADT) \cite{adt} in Haskell we use an
inductive family \cite{indfam} to define the \iagda{Tree} type, this time
including the \iagda{Color} (of the root node) and \iagda{Height} (number of
black nodes on a path from the root to an empty node) in the type.
This type enforces both red-black tree invariants: the constructor for a
red node only allows children with a black root therefore preventing a red
node from having a red parent which is invariant 1, the constructors for red
and black nodes demand their children be of the same height thereby enforcing
invariant 2.
Now the type system guarantees that our functions always produce valid
red-black trees.
If we define a function that tries to create an invalid \iagda{Tree} the code
will not type check.
As a side effect type checking becomes part of writing code, instead of writing
a large part of the program and then type checking at compile time.
Agda integrates type checking into the editor which facilitates a more
interactive workflow, alternately writing some code and type checking, making
use of feedback from the type checker to correct the code.

The biggest advantage of this stronger, more precise type is also somewhat of
a disadvantage at the same time.
Oftentimes an algorithm will temporarily violate invariants because it is
easier to mess up and restore than do everything right immediately, especially
when changes can cascade.
Okasaki makes use of this in his \ihask{ins} function: \ihask{ins} may return
an invalid \ihask{Tree} which is why the calls only occur as part of a call
to \ihask{makeBlack} or \ihask{balance}.
Because of how \ihask{ins} is implemented it may return a \ihask{Tree} with a
red root that has one red child, this is also called an \emph{infrared} tree.
Transforming an infrared tree into a valid tree can be done by blackening
(just changing the color to black) the root node, however this increases the
height of the tree by one.
During insertion a height increase is undesirable because then the tree has to
be rebalanced.
Since the tree has to be rebalanced anyway, this is what happens in \ihask{ins}.
\ihask{ins} recurses by deconstructing the node and then deciding which child
the new value belongs to.
If this child is infrared and the current node is black Okasaki's rebalancing
algorithm restores the red-black tree invariants, this is where the elegance
of his solution comes from.
It is also the only way such an imbalance can occur because a child can only be
infrared if it had a red root before the insertion which means the node that
was just deconstructed is black so every time we need to rebalance there is a
black node with an infrared child, of course this relies on the assumption that
the original tree was a valid red-black tree.
Since this kind of temporary violation of invariants is not allowed by the
dependently typed implementation we need a new data type to represent infrared
trees:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  data IRTree : Height → Set a where
    IRl : ∀{h} → Tree R h → A → Tree B h
      → IRTree h
    IRr : ∀{h} → Tree B h → A → Tree R h
      → IRTree h
\end{minted}

Because the type for an infrared tree and a red-black tree are now distinct we
cannot define \iagda{balance} as Okasaki did because the left tree can be
infrared and the right red-black or the other way around.
However we can easily mimick this behaviour using another data type:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  data OutOfBalance : Height → Set a where
    _◂_◂_ : ∀{c h} → IRTree h → A → Tree c h
            → OutOfBalance h
    _▸_▸_ : ∀{c h} → Tree c h → A → IRTree h
            → OutOfBalance h
\end{minted}

Thanks to Agda's flexible mixfix syntax we can now write this haskell code
\ihask{balance B infra b c} turns into this Agda code
\iagda{balance (infra ◂ b ◂ c)}, notice also that we do not require the color
argument we will come back to this.
There's still one type missing, some of Okasaki's functions like \ihask{ins}
can return either a red-black or an infrared tree.
Because those are represented by two separate types we need an extra type to
act as a disjoint sum:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  data Treeish : Color → Height → Set a where
    RB : ∀{c h} → Tree c h → Treeish c h
    IR : ∀{h} → IRTree h → Treeish R h
\end{minted}

Being more precise takes more effort, one data type is replaced by four.
The increase in precision goes hand in hand with an increase of verbosity this
will also be apparent in the implementation of the functions.
The first function to consider is balance because this is the main source of
elegance:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  balance : ∀{h} → OutOfBalance h → Tree R (suc h)
  balance (IRl (R a x b) y c ◂ z ◂ d) =
    R (B a x b) y (B c z d)
  balance (IRr a x (R b y c) ◂ z ◂ d) =
    R (B a x b) y (B c z d)
  balance (a ▸ x ▸ IRl (R b y c) z d) =
    R (B a x b) y (B c z d)
  balance (a ▸ x ▸ IRr b y (R c z d)) =
    R (B a x b) y (B c z d)
\end{minted}

This is fairly similar to Okasaki's implementation, arguably it is more
elegant, but there are subtle differences.
The color argument is no longer necessary, it could be replaced by a pattern
match on the constructor.
The reason for this as well as the removal of the \emph{catch-all} clause is
that the type no longer permits a valid red-black tree.
In Okasaki's implementation, when \ihask{balance} was called with a valid
red-black tree it would be returned unaltered, that way the function can be
called indiscriminately.
With the more restricted implementation it is necessary to ensure
\iagda{balance} is only called when necessary.
Often this requires more extensive case analysis but this can be a good thing
because if there's a bug in Okasaki's implementation it can be harder to find
since every \ihask{Tree} could go through \ihask{balance} which means we'd
have to check that \ihask{balance} has the right behaviour even for trees that
shouldn't even be rebalanced in the first place.

The local functions Okasaki declared in \ihask{insert} have changed a lot more.
They are not defined locally because we need multiple clauses for
\iagda{insert}.
The simplest of these is \iagda{blacken}, similar to Okasaki's
\ihask{makeBlack}:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  blacken : ∀{c h} → (Treeish c h)
            → (if c =ᶜ B then Tree B h
                         else Tree B (suc h))
  blacken (RB E) = E
  blacken (RB (R l b r)) = (B l b r)
  blacken (RB (B l b r)) = (B l b r)
  blacken (IR (IRl l b r)) = (B l b r)
  blacken (IR (IRr l b r)) = (B l b r)
\end{minted}

All \iagda{blacken} does is take a tree and make the root node black.
However, this function has to handle valid red-black trees as well as infrared
trees (which corresponds to \iagda{Treeish}) and for each of these we have to
consider each constructor because we need to access the values used to
construct the root node.
When a red root is colored black the \iagda{Height} of the \iagda{Tree}
increases by one, that's why we have a return type that is conditional on the
color of the original \iagda{Tree}.

Because \iagda{ins} is no longer defined locally we need an explicit argument
for the value to be inserted.
\iagda{ins} takes a red-black \iagda{Tree} and returns a \iagda{Tree} if the
given \iagda{Tree} was black or a possibly infrared \iagda{Treeish} if it was
not.
Either of these would have the same \iagda{Height} which can be guaranteed only
because the function can return infrared trees.
The color of the resulting tree is left undecided by using a dependent pair,
this does mean we have to return a pair of a \iagda{Color} and a \iagda{Tree}.
Okasaki's rebalancing algorithm can't balance infrared trees by themselves but
it doesn't need to because an infrared tree can be turned into a red-black tree
by coloring the root black:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  ins : ∀{c h} → (a : A) → (t : Tree c h)
        → Σ[ c' ∈ Color ]
           (if c =ᶜ B then (Tree c' h)
                      else (Treeish c' h))
  ins a E = R , R E a E
  --
  ins a (R _ b _) with a ≤ b
  ins a (R l _ _) | LT with ins a l
  ins _ (R _ b r) | LT | R , t =
    R , IR (IRl t b r)
  ins _ (R _ b r) | LT | B , t =
    R , (RB (R t b r))
  ins _ (R l b r) | EQ = R , RB (R l b r)
  ins a (R _ _ r) | GT with ins a r
  ins _ (R l b _) | GT | R , t =
    R , (IR (IRr l b t))
  ins _ (R l b _) | GT | B , t =
    R , (RB (R l b t))
  --
  ins a (B _ b _) with a ≤ b
  ins a (B l _ _) | LT with ins a l
  ins _ (B {R} _ b r) | LT | c , RB t =
    B , B t b r
  ins _ (B {R} _ b r) | LT | .R , IR t =
    R , balance (t ◂ b ◂ r)
  ins _ (B {B} _ b r) | LT | c , t =
    B , B t b r
  ins _ (B l b r) | EQ = B , B l b r
  ins a (B _ _ r) | GT with ins a r
  ins _ (B {cr = R} l b _) | GT | c , RB t =
    B , B l b t
  ins _ (B {cr = R} l b _) | GT | .R , IR t =
    R , balance (l ▸ b ▸ t)
  ins _ (B {cr = B} l b _) | GT | c , t =
    B , B l b t
\end{minted}

\iagda{ins} is a lot longer in this implementation, mostly because we need to
be more precise.
Whether we return a red-black or an infrared tree and whether we should call
\iagda{balance} or not, matters.
The \iagda{with} construct enables pattern matching on intermediate results to
decide which child a value belongs in and if the intermediate \iagda{Tree}
needs to be rebalanced or not.

The \iagda{insert} function has a more precise type than Okasaki's
implementation but it is not as precise as possible, i.e. the resulting
\iagda{Tree} cannot have just any \iagda{Height} but it is not uniquely defined
either.
The constructors for the disjoint sum have been aliased to make it more obvious
what the resulting type is, either the resulting \iagda{Tree} has the same
\iagda{Height} (h+0) or the \iagda{Height} has increased by one (h+1).
Returning a value as part of a disjoint sum here is a concession, the problem
with being more precise is that it is difficult to say in advance whether a
given \iagda{Tree} will increase in \iagda{Height} upon inserting a given value.
A function that decides this can easily be defined and used in the type of
\iagda{insert} but Agda cannot reason about this function which gives problems
because Agda only allows total functions.
If such a function is part of the type Agda cannot infer when certain results
of the function are impossible and this combined with the necessity to cover
all cases that Agda can't rule out makes this very difficult.
This can probably be remedied by providing Agda with an appropriate proof but
this is a trade-off that has to be made, considering the added effort of a
proof against a small increase in precision.
In this case simplicity was chosen over precision:

%----------------------------------------%
\begin{minted}[fontsize=\small]{agda}
  insert : ∀{c h} → (a : A) → (t : Tree c h)
           → Tree B h ⊎ Tree B (suc h)
  insert {R} a t with ins a t
  ... | R , t' = h+1 (blacken t')
  ... | B , t' = h+0 (blacken t')
  insert {B} a t with ins a t
  ... | R , t' = h+1 (blacken (RB t'))
  ... | B , t' = h+0 (blacken (RB t'))
\end{minted}

\iagda{insert} takes a value and a red-black \iagda{Tree} and returns a black
\iagda{Tree} of the same \iagda{Height} or one level higher.
The cases for a red \iagda{Tree} or a black \iagda{Tree} are split up because
\iagda{ins} respectively returns a \iagda{Tree} or a red-black \iagda{Treeish}.


\section{Red-Black Trees in Haskell}

Haskell as a language is certainly not dependently typed but GHC \cite{ghc} has
several extensions that try to retrofit similar features into the
Hindley-Milner type system \cite{hindley}.
One of Haskell's most important features is the type inference of its type
system, this has to be preserved by whatever extension may be added.
Dependent types in general do not allow for type inference, Agda for example
has no type inference.
This implies that Haskell will never be a fully dependently typed system.
This also means that from time to time we will have to jump through hoops to
achieve similar guarantees from the type system.
In this section we will see how we can achieve most of the benefits of the
implementation in the last section in a not quite dependently typed language.
The code in this section relies on the following GHC extensions: GADTs
\cite{gadts}, DataKinds \cite{datakinds} and KindSignatures
\cite{kindsigs}.
These extensions are attempts to make the Haskell type system more expressive
without giving up on type inference.
The \ihask{Tree} data type is fairly similar:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  data Nat = Z | S Nat
    deriving (Show, Eq, Ord)

  data Color = R | B
    deriving (Show, Eq)

  data Tree :: Color -> Nat -> * -> * where
    ET :: Tree B Z a
    RT :: Tree B  h a -> a -> Tree B  h a
          -> Tree R h a
    BT :: Tree cl h a -> a -> Tree cr h a
          -> Tree B (S h) a
\end{minted}

Other than syntax and constructor names, the most visible difference is the
extra parameter for polymorphism.
Agda's type system is not polymorphic, implicit arguments allow a very
similar approach.
In the previous implementation this problem as well as making sure the elements
that are put in trees can be compared, was solved in a different way
through Agda's module system but that would take us too far.
In Haskell we can use and \ihask{Ord} constraint when we need to be able to
compare elements.
The \ihask{Tree} data type is not an ordinary ADT but a generalised algebraic
data type (GADT).
A GADT enables the specification of the type signature of each constructor
individually, this also permits the definition of constructors that result in a
value of a more restricted type.
There's also a kind \cite{kind} signature on \ihask{Tree}, just as a value has
a type, a type in Haskell has a kind.
Most types in Haskell have kind \ihask{*} and without extensions it is actually
impossible to define new kinds.
The kind signature for \ihask{Tree} indicates that \ihask{Tree} is a type, the
resulting kind is \ihask{*}, that takes three parameters, i.e. one of kind
\ihask{Color}, one of kind \ihask{Nat} and one of kind \ihask{*}.
The \ihask{Color} and \ihask{Nat} kinds are not defined explicitly, they are
created by data type promotion \cite{dtprom} through the DataKinds extension.
The type \ihask{Color} is promoted to the kind \ihask{Color} and the
constructors \ihask{R} and \ihask{B} are promoted to the types \ihask{R} and
\ihask{B}, any of the promoted objects can be preceded by a single quote, e.g.
\verb|'Color| if there is a possibility of ambiguity.

Because the \ihask{Tree} type is more specific than in Okasaki's implementation
we run into the same problem as in the last section and we need several
additional types:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  data IRTree :: Nat -> * -> * where
    IRl :: Tree R h a -> a -> Tree B h a
           -> IRTree h a
    IRr :: Tree B h a -> a -> Tree R h a
           -> IRTree h a

  data OutOfBalance :: Nat -> * -> * where
    (:<:) :: IRTree h a -> a -> Tree c h a
             -> OutOfBalance h a
    (:>:) :: Tree c h a -> a -> IRTree h a
             -> OutOfBalance h a

  data Treeish :: Color -> Nat -> * -> * where
    RB :: Tree c h a -> Treeish c h a
    IR :: IRTree h a -> Treeish R h a
\end{minted}

Except for minor differences, i.e. these are GADTs instead of inductive
families, polymorphism and not allowing mixfix notation, these types are the
same as the ones in Agda.

The \ihask{balance} function is also very similar:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  balance :: OutOfBalance h a -> Tree R (S h) a
  balance ((:<:) (IRl (RT a x b) y c) z d) =
    RT (BT a x b) y (BT c z d)
  balance ((:<:) (IRr a x (RT b y c)) z d) =
    RT (BT a x b) y (BT c z d)
  balance ((:>:) a x (IRl (RT b y c) z d)) =
    RT (BT a x b) y (BT c z d)
  balance ((:>:) a x (IRr b y (RT c z d))) =
    RT (BT a x b) y (BT c z d)
\end{minted}

\ihask{blacken} has a slightly less specific type.
Functions that operate on the type level are a lot more restricted in Haskell,
precisely because there is a type level, whereas in Agda all types are values.
Type families \cite{typefam} are a GHC extension that provides something like a
function at the type level but for simplicity's sake we will just relax the
type:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  blacken :: Treeish c h a
             -> Either (Tree B h a)
                       (Tree B (S h) a)
  blacken (RB ET) = Left ET
  blacken (RB (RT l b r)) = Right (BT l b r)
  blacken (RB (BT l b r)) = Left (BT l b r)
  blacken (IR (IRl l b r)) = Right (BT l b r)
  blacken (IR (IRr l b r)) = Right (BT l b r)
\end{minted}

The return type is now a disjoint sum, this means the result has to be
explicitly tagged as having remained the same height or having increased by
one.

The \ihask{ins} function also has a slightly relaxed type in that we always
return a \ihask{Treeish}.
The disjoint sum with \ihask{Either} in this case replaces the existential
quantification of the dependent pair in the Agda implementation.
Of course a disjoint sum can only replace such a dependent pair if the type we
want to quantify over has exactly two elements.
Because this function needs to decide where an element belongs in the
\ihask{Tree} there is an \ihask{Ord} constraint on the type variable
representing the element type:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  ins :: Ord a => a -> Tree c h a
         -> Either (Treeish R h a)
                   (Treeish B h a)
  ins a ET = Left $ RB (RT ET a ET)
  --
  ins a (RT l b r)
    | a < b , Left (RB t) <- ins a l =
      Left $ IR (IRl t b r)
    | a < b , Right (RB t) <- ins a l =
      Left $ RB (RT t b r)
    | a == b = Left $ RB (RT l b r)
    | a > b , Left (RB t) <- ins a r =
      Left $ IR (IRr l b t)
    | a > b , Right (RB t) <- ins a r =
      Left $ RB (RT l b t)
  --
  ins a (BT l b r)
    | a < b , Left (RB t) <- ins a l =
      Right $ RB (BT t b r)
    | a < b , Left (IR t) <- ins a l =
      Left $ RB (balance ((:<:) t b r))
    | a < b , Right (RB t) <- ins a l =
      Right $ RB (BT t b r)
    | a == b = Right $ RB (BT l b r)
    | a > b , Left (RB t) <- ins a r =
      Right $ RB (BT l b t)
    | a > b , Left (IR t) <- ins a r =
      Left $ RB (balance ((:>:) l b t))
    | a > b , Right (RB t) <- ins a r =
      Right $ RB (BT l b t)
\end{minted}

\ihask{ins} makes use of pattern guards \cite{patguard} to unwrap the
\ihask{Tree} or \ihask{IRTree} from the tagged \ihask{Treeish} \ihask{ins} now
invariably returns.
The return type always being a \ihask{Treeish} would be problematic in Agda.
Because Agda is a total language, if a function returns a \iagda{Treeish} and
you want to pattern match on the result, you \emph{have} to provide patterns
that cover each possible value of that type.
In the case of \iagda{ins} this would mean that you'd have to cover infrared
trees as a result of each of the recursive calls even though a call to
\iagda{ins} on a \iagda{Tree} with a black root can never result in an infrared
tree.
What can we return as a result for a case that can not possibly occur?
It doesn't really matter what it is as long as it's a valid value of the return
type of the function.
However, you can't extract the \iagda{Height} from the \iagda{Tree} because
that information is in the type not the value.
Creating a \iagda{Tree} of the right \iagda{Height} would have to be done
recursively while deconstructing the \iagda{Tree}, which would require an
entire new function just to cover a case that will never occur.
In Agda it clearly makes more sense to restrict the type.
Haskell is a partial language so we don't have to worry about cases we know
will never occur, of course without the totality check we might miss cases that
actually can occur.

The \ihask{insert} function is simpler this time because \ihask{ins} will
always return a \ihask{Treeish}.
The type is the same as for the Agda implementation, glossing over the
\ihask{Ord} constraint and polymorphism.
Again a pattern guard unwraps the intermediate result before passing it on to
\ihask{blacken}:

%----------------------------------------%
\begin{minted}[fontsize=\small]{haskell}
  insert :: Ord a => a -> Tree c h a
            -> Either (Tree B h a)
                      (Tree B (S h) a)
  insert a t
    | Left t' <- ins a t = blacken t'
    | Right t' <- ins a t = blacken t'
\end{minted}


$
\section{Conclusion}
Dependent types are expressive enough to allow for the specification of complex
properties.
These properties are then statically verified by the type system, thereby
preventing bugs.
Agda because of its design as a fully dependently typed language allows us to
specify arbitrarily complex properties about our code without much friction.
Haskell on the other hand does allow us to specify interesting properties but
it is clear that the necessary functionality comes from extensions and is not
as neatly accommodated by the language.


TODO: title, conclusion, references, code in appendices(?), acknowledgements

\appendices
\section{Proof of the First Zonklar Equation}
Appendix one text goes here.

% use section* for acknowledgement
\section*{Acknowledgment}


The authors would like to thank...


% trigger a \newpage just before the given reference
% number - used to balance the columns on the last page
% adjust value as needed - may need to be readjusted if
% the document is modified later
%\IEEEtriggeratref{8}
% The "triggered" command can be changed if desired:
%\IEEEtriggercmd{\enlargethispage{-5in}}

\bibliographystyle{IEEEtran}
\bibliography{./sciartt}

\end{document}


